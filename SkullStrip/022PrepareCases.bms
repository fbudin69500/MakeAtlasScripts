### The script uses:
#- ImageMath
#- ResampleVolume2
###############################
#prepare files before computation
#rreg and areg need .gipl.gz files so we convert everything that
#these softare are going to use to this format

#convert
if( ${Registration} == TRUE )
  Set( Img ${SubjDir}/${INPUT_SUB_DIR}/${Case}${RIGID_REGISTRATION_IMAGE_SUFFIX} )
  Set( DestImg ${tempDir}/${Case}/${Case}${RIGID_REGISTRATION_IMAGE_TAG}_noDirection.gipl.gz )

  if( ${IS_SCALAR} == FALSE )
    echo( "DTI image" )
    if( ${RIGID_IS_MD} == TRUE )
      echo( "MD image" )
      set( Cmd ${ImageMathDIR}ImageMath ${Img} -constOper 2,100000 -outfile ${DestImg} )
    else( ${RIGID_IS_MD} == TRUE )
      if( ${RIGID_IS_FA} == TRUE )
         echo( "FA image" )
         set( Cmd ${ImageMathDIR}ImageMath ${Img} -constOper 2,10000 -outfile ${DestImg} )
      else( ${RIGID_IS_FA} == TRUE )
         Set( Cmd ${ResampleVolume2DIR}ResampleVolume2 ${Img} ${DestImg} -i nn )
      EndIf( ${RIGID_IS_FA} == TRUE )
    EndIf( ${RIGID_IS_MD} == TRUE )
  else( ${IS_SCALAR} == FALSE )
    echo( "Scalar image" )
    Set( Cmd ${ResampleVolume2DIR}ResampleVolume2 ${Img} ${DestImg} -i nn )
  endif( ${IS_SCALAR} == FALSE )
  Run (output ${Cmd})
  Echo (${output})

  Set( Direction ${tempDir}/${Case}/${Case}_direction.tfm )
  Set( Cmd ${ITKTransformToolsDIR}ITKTransformTools direction ${Img} ${Direction} )
  Run( output ${Cmd} )
  echo( ${output} )

  Set( directionGrid ${tempDir}/${Case}/${Case}_directionGrid.nrrd )
  Set( Cmd ${ITKTransformToolsDIR}ITKTransformTools size ${DestImg} ${Direction} --grid ${directionGrid} )
  Run( output ${Cmd} )
  echo( ${output} )

  Set( directionIMG ${tempDir}/${Case}/${Case}${RIGID_REGISTRATION_IMAGE_TAG}.gipl.gz )
  Set( Cmd ${ResampleVolume2DIR}ResampleVolume2 ${DestImg} -f ${Direction} -R ${directionGrid} -d 1,0,0,0,1,0,0,0,1 ${directionIMG} )
  Run( output ${Cmd} )
  echo( ${output} )
EndIf( ${Registration} == TRUE )




set( count 0 )
If( ${IS_SCALED} == TRUE )
  ForEach( file ${ATLAS_TAG} )
    GetParam(EXT ${ATLAS_EXT} ${count})
    Set( SrcImg ${ATLAS_DIR}/${file}${EXT} )
    if( ${Registration} == TRUE )
      Set( DstImg ${tempDir}/${Case}/${file}.gipl.gz )
    else( ${Registration} == TRUE )
      Set( DstImg ${tempDir}/${Case}/${file}.mha )
    endif( ${Registration} == TRUE )
    Set (transformCmd ${ResampleVolume2DIR}ResampleVolume2 ${SrcImg} ${DstImg} -R ${SubjDir}/${INPUT_SUB_DIR}/${ATLAS_GRID} )
    echo(${transformCmd})
    Run (output ${transformCmd})
    Echo (${output})
    Set( ImageMathCmd ${ImageMathDIR}ImageMath ${DstImg} -changeSp 1,1,1 -outfile ${DstImg} -type float )
    echo( ${ImageMathCmd} )
    Run( output ${ImageMathCmd} )
    echo( ${output} )
    Inc( ${count} 1 )
    Int( ${count} )
  EndForEach( file ${ATLAS_TAG} )
EndIf( ${IS_SCALED} )
