#!/bin/tcsh
if( $# != 2 ) then
  echo "Usage: $0 ConfigPath KillDevilProjectPath"
  exit(-1)
endif

set killdeviltoolspath=/nas02/home/f/b/fbudin/tools/
set NbOfCoresForAverage=12
set antstoolspath=/nas02/home/f/b/fbudin/tools/ANTs-1.9.x-Linux/bin/ #do not forget the trailing '/'

echo "Configures..."
set configPath=$1
set lastchar = `echo -n $configPath | tail -c -1`
if( "$lastchar" == "/" ) then
  set configPath=`echo -n $configPath | head -c -1`
endif
set killdevilPath=$2
set lastchar = `echo -n $killdevilPath | tail -c -1`
if( "$lastchar" == "/" ) then
  set killdevilPath=`echo -n $killdevilPath | head -c -1`
endif
echo "###Killdevil Path: $killdevilPath"
set filepath = $0:h
echo "###Current script path: "$filepath
set configPath=`$filepath/local-scripts/GetStringForCMakeCache.py $configPath "ConfigDirectory"`
echo "###Config Path: $configPath"
set MakeAtlasPath=`$filepath/local-scripts/GetStringForCMakeCache.py $configPath "MakeAtlasScripts_SOURCE_DIR"`
echo "###Make Atlas Scripts Path: $MakeAtlasPath"
set ProcessingPath=`$filepath/local-scripts/GetStringForCMakeCache.py $configPath "ProcessingDirectory"`
echo "###Processing Path: $ProcessingPath"
$filepath/local-scripts/VerifyProcessingPathIsNotConfigPath.py $ProcessingPath $configPath
set result=$?
if(  $result != 0 ) then
  exit(-1)
endif
if(! -d $configPath/Killdevil-config) then
  mkdir $configPath/Killdevil-config
endif


set currentPath = `pwd`
set MakeAtlasDirectory = $MakeAtlasPath:t

set first = `echo $configPath | head -c 1`
if( "$first" != "/" ) then
  set configPath = $currentPath/$configPath
endif

echo "###Absolute Config Path: $configPath"

set projectDirectory0=`$filepath/local-scripts/GetProjectDirectory.py $configPath $ProcessingPath 0`
set projectDirectory1=`$filepath/local-scripts/GetProjectDirectory.py $configPath $ProcessingPath 1`
set projectDirectory2=`$filepath/local-scripts/GetProjectDirectory.py $configPath $ProcessingPath 2`
$filepath/local-scripts/MakeAtlasIsInProjectDirectory.py $MakeAtlasPath $projectDirectory0
if( $? ) then
  exit(-1)
endif

if(! -d $configPath/Killdevil-config/AllCases) then
  mkdir $configPath/Killdevil-config/AllCases
endif

set projectDirectory1For_sed=`echo $projectDirectory1 | sed "s/\//\\\//g"`
set ConfigANDToolsDirectory = `echo $configPath |sed "s/$projectDirectory1For_sed\///"`
set Tools = `echo $ConfigANDToolsDirectory |sed "s/\/.*//g"`
set ConfigDirectory = `echo $ConfigANDToolsDirectory |sed "s/$Tools\///g"`
set ConfigDirectoryFor_sed=`echo $ConfigDirectory | sed "s/\//\\\//g"`
echo "###Config Directory (subdirectory in which the configured scripts are written): "$ConfigDirectory
echo "###Tools Directory (subdirectory in which the tools and templates are copied): "$Tools
set configPathFor_sed=`echo $configPath | sed "s/\//\\\//g"`
set killdevilconfigpath=$killdevilPath/$Tools/$ConfigDirectory/Killdevil-config
set killdevilconfigpathFor_sed=`echo $killdevilconfigpath | sed "s/\//\\\//g"`
set killdevilscriptspath=$killdevilconfigpath/killdevil-scripts
set killdevilscriptspathFor_sed=`echo $killdevilscriptspath | sed "s/\//\\\//g"`
set killdevilpathFor_sed=`echo $killdevilPath | sed "s/\//\\\//g"`
set projectdirectoryFor_sed=`echo $projectDirectory0 | sed "s/\//\\\//g"`
set ProcessingPathFor_sed=`echo $ProcessingPath | sed "s/\//\\\//g"`
set processingDir = `echo $ProcessingPath |sed "s/$projectDirectory1For_sed\///g"`
set killdevilProcessingPath = $killdevilPath/$processingDir
set killdevilProcessingPathFor_sed=`echo $killdevilProcessingPath | sed "s/\//\\\//g"`

set first = `echo $filepath | head -c 1`
if( "$first" != "/" ) then
  set filepath = $currentPath/$filepath
endif
set filepathFor_sed=`echo $filepath | sed "s/\//\\\//g"`
foreach i ($MakeAtlasPath/Killdevil/*.script.in)
  set name=$i:t:r
  set outputFileName=$configPath/Killdevil-config/$name
  sed "s/@CONFIG_PATH@/$configPathFor_sed/" $i > $outputFileName
  sed -i "s/@KILLDEVILSCRIPTSPATH@/$killdevilscriptspathFor_sed/" $outputFileName
  sed -i "s/@KILLDEVILCONFIGPATH@/$killdevilconfigpathFor_sed/" $outputFileName
  sed -i "s/@KILLDEVILPATH@/$killdevilpathFor_sed/" $outputFileName
  sed -i "s/@PROJECTDIRECTORY@/$projectdirectoryFor_sed/" $outputFileName
  sed -i "s/@TOOLS@/$Tools/" $outputFileName
  sed -i "s/@SCRIPTSPATH@/$filepathFor_sed/" $outputFileName
  sed -i "s/@PROCESSINGPATH@/$ProcessingPathFor_sed/" $outputFileName
  sed -i "s/@KILLDEVILPROCESSINGPATH@/$killdevilProcessingPathFor_sed/" $outputFileName
  chmod +x $outputFileName
end

if(! -d $configPath/Killdevil-config/killdevil-scripts) then
  mkdir $configPath/Killdevil-config/killdevil-scripts
endif
set killdeviltoolspathFor_sed=`echo $killdeviltoolspath | sed "s/\//\\\//g"`
set antstoolspath_sed=`echo $antstoolspath | sed "s/\//\\\//g"`
foreach i ($MakeAtlasPath/Killdevil/killdevil-scripts/*.script.in)
  set name=$i:t:r
  set outputFileName=$configPath/Killdevil-config/killdevil-scripts/$name
  sed "s/@KILLDEVILTOOLSPATH@/$killdeviltoolspathFor_sed/" $i > $outputFileName
  sed -i "s/@NUMBEROFCORESFORAVERAGE@/$NbOfCoresForAverage/" $outputFileName
  sed -i "s/@ANTSTOOLSPATH@/$antstoolspath_sed/" $outputFileName
  chmod +x $outputFileName
end

set MakeAtlasPathFor_sed=`echo $MakeAtlasPath | sed "s/\//\\\//g"`
foreach i (${configPath}/*-*bms)
  set oneFileName=$i
  set name = $i:t
  $filepath/local-scripts/ReplacePaths.py $i $configPath/Killdevil-config/AllCases/${name}.1 $projectDirectory0 $killdevilPath
  $filepath/local-scripts/ReplacePaths.py $configPath/Killdevil-config/AllCases/${name}.1 $configPath/Killdevil-config/AllCases/${name}.2 $projectDirectory1 $killdevilPath
  $filepath/local-scripts/ReplacePaths.py $configPath/Killdevil-config/AllCases/${name}.2 $configPath/Killdevil-config/AllCases/${name} $projectDirectory2 $killdevilPath
  sed -i "s/.*GlobalConfig.bms/include( $killdevilpathFor_sed\/$Tools\/$ConfigDirectory\/Killdevil-config\/AllCases\/GlobalConfig.bms/" $configPath/Killdevil-config/AllCases/${name}
  rm $configPath/Killdevil-config/AllCases/${name}.1 $configPath/Killdevil-config/AllCases/${name}.2
end
sed "s/-j 8/-j $NbOfCoresForAverage/" $MakeAtlasPath/4.1-AverageCreation-ANTS/runANTSatlasScript.tcsh > $configPath/Killdevil-config/AllCases/runANTSatlasScript.tcsh
chmod +x $configPath/Killdevil-config/AllCases/runANTSatlasScript.tcsh
sed -i "s/ANTStcshScriptPATH.*/ANTStcshScriptPATH $killdevilconfigpathFor_sed\/AllCases\/runANTSatlasScript.tcsh)/" $configPath/Killdevil-config/AllCases/4.1-AverageCreation-ANTSConfig.bms
sed -i "s/GreedyAtlasNbOfThreads 8/GreedyAtlasNbOfThreads $NbOfCoresForAverage/" $configPath/Killdevil-config/AllCases/4-AverageCreationConfig.bms
$filepath/local-scripts/GlobalConfig.py $MakeAtlasPath/Config/GlobalConfig.bms.in $configPath/Killdevil-config/AllCases/GlobalConfig.bms $killdeviltoolspath



if(! -d $configPath/Killdevil-config/SeparatedCases) then
  mkdir $configPath/Killdevil-config/SeparatedCases
endif
$filepath/local-scripts/WriteCaseListInFile.py $oneFileName $configPath/Killdevil-config/listCases.txt
foreach i ($configPath/Killdevil-config/AllCases/*-*bms)
  set name = $i:t:r
  if(! -d $configPath/Killdevil-config/SeparatedCases/$name) then
    mkdir $configPath/Killdevil-config/SeparatedCases/$name
  endif
  $filepath/local-scripts/WriteConfigFiles.script $i $configPath/Killdevil-config/listCases.txt $configPath/Killdevil-config/SeparatedCases/$name $killdevilPath $Tools/$ConfigDirectory/Killdevil-config
end

rsync --exclude 'Data' --exclude 'temp' --exclude '.git' -r -a -v -z -e ssh $projectDirectory0/ killdevil.unc.edu:$killdevilPath
