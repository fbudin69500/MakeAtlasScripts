GetParamCount( Average_count ${Average_INPUT_SUFFIX} )
GetParamCount( HM_count ${HISTOGRAM_MATCH} )
If( ${HM_count} != ${Average_count} )
  echo( 'Average_INPUT_SUFFIX and HISTOGRAM_MATCH must have the same number of parameters' )
  exit()
EndIf( ${HM_count} != ${Average_count} )

MakeDirectory( ${tempDir} )
GetParam( first_case ${CASES} 0 )

MakeDirectory( ${outputDir} )
###Save script config
If( ${ScriptName} != '' )
  GetFilename( name ${ScriptName} NAME )
  CopyFile( ${ScriptName} ${outputDir}/${name} )
EndIf( ${ScriptName} != '' )

set( posAv 0 )
ForEach( j ${Average_INPUT_SUFFIX} )
  GetFilename( Average_TAG ${j} NAME_WITHOUT_EXTENSION )
  GetParam( Current_HM ${HISTOGRAM_MATCH} ${posAv} )
  set( posCase 0 )
  set( FirstImage ${dir}/${INPUT_PRE_SUBDIR}/${first_case}/${INPUT_POST_SUBDIR}/${first_case}${j} )

  ForEach( i ${CASES} )
    set (OutSubjDir ${dir}/${OUTPUT_PRE_SUBDIR}/${i})
    set (InSubjDir ${dir}/${INPUT_PRE_SUBDIR}/${i}/${INPUT_POST_SUBDIR} )
    ###Mask images
    set( MASK ${InSubjDir}/${i}${NEW_MASK_TAG} )#we save mask in case directory
    set( InputImage ${InSubjDir}/${i}${j} )
    set( inTAG _masked )
    set( maskedInput ${tempDir}/${i}${Average_TAG}${inTAG}.nii.gz )
    set( Cmd ${ImageMath} ${InputImage} -mask ${MASK} -outfile ${maskedInput} -type float)
    echo( ${Cmd} )
    If( ${step} < 2 )
      run( output ${Cmd} )
      echo( ${output} )
    EndIf( ${step} < 2 )
    ###Median Filter
    If( ${Mradius} != 0 )
      set(outTAG _median )
      set( MedianImage ${tempDir}/${i}${Average_TAG}${outTAG}.nii.gz )  
      set( Cmd ${MedianImageFilter} --neighborhood ${Mradius},${Mradius},${Mradius} ${maskedInput} ${MedianImage} )
      echo( ${Cmd} )
      If( ${step} < 3 )
        run( output ${Cmd} )
        echo( ${output} )
      EndIf( ${step} < 3 )
      set( inTAG ${outTAG} )
    EndIf( ${Mradius} != 0 )
    ###HistogramMatch
    If( ${Current_HM} == TRUE )
      set( outTAG ${inTAG}_hm )
      set( IMG ${tempDir}/${i}${Average_TAG}${inTAG}.nii.gz )
      set( MOVING_IMG ${tempDir}/${i}${Average_TAG}${outTAG}.nii.gz )
      set( Cmd ${ImageMath} ${IMG} -matchHistogram ${FirstImage} -outfile ${MOVING_IMG} -type float)
      echo( ${Cmd} )
      If( ${step} < 4 )
        run( output ${Cmd} )
        echo( ${output} )
      EndIf( ${step} < 4 )
      set( inTAG ${outTAG} )
    EndIf( ${Current_HM} == TRUE )
    If( ${TRANSFORM_SUFFIX} != '' )
      set( RigidTransform -f ${InSubjDir}/${i}${TRANSFORM_SUFFIX} --transform_order input-to-output )
    EndIf( ${TRANSFORM_SUFFIX} != '' )
    Set( hfield ${InSubjDir}/${i}${HField_Suffix} )
    set( outTAG ${inTAG}_transformed )
    set( transformedImage ${tempDir}/${i}${Average_TAG}${outTAG}.nii.gz )
    set( Cmd ${ResampleVolume2} ${tempDir}/${i}${Average_TAG}${inTAG}.nii.gz ${transformedImage} -H ${hfield} -R ${PopAverageDir}/${Reference_Image} ${RigidTransform} )
    echo( ${Cmd} )
    If( ${step} < 5 )
      run( output ${Cmd} )
      echo( ${output} )
    EndIf( ${step} < 5 )
    set( inTAG ${outTAG} )

    if( ${posCase} == 0 )
      Set( FIRST_IMAGE ${transformedImage} )
    else( ${posCase} == 0 )
      Set( LIST_IMAGES ${transformedImage} ${LIST_IMAGES} )
    endif( ${posCase} == 0 )

    Inc( ${posCase} 1 )
    Int( ${posCase} )
  EndForeach( ${CASES} )
  set( AverageOutput ${outputDir}/AverageImage${Average_TAG}.nii.gz )
  set( Cmd ${ImageMath} ${FIRST_IMAGE} -avg ${LIST_IMAGES} -outfile ${AverageOutput} -type float )
  If( ${step} < 6 )
    Run( output ${Cmd} )
    echo( ${output} )
  EndIf( ${step} < 6 )

  Set( LIST_IMAGES '' )
  Inc( ${posAv} 1 )
  Int( ${posAv} )
EndForEach( ${Average_INPUT_SUFFIX} )
