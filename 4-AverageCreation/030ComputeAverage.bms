### The script uses:
#- ANTS
#- Double2FloatTransform
#- AtlasWerks
#- ImageMath
#- ResampleVolume2
#- BRAINSDemonWarp
#- ITKTransformTools
#########################Mask image used to compute average#############
MakeDirectory( ${tempDir} )
foreach( i ${CASES} )
  set (OutSubjDir ${dir}/${OUTPUT_PRE_SUBDIR}/${i})
  set (InSubjDir ${dir}/${INPUT_PRE_SUBDIR}/${i}/${INPUT_POST_SUBDIR} )
  MakeDirectory( ${OutSubjDir}/${PROCESS_SUBDIR} )
  ##Save config file in each sub dir of each case
  if( ${ScriptName} != '' )
    GetFilename( name ${ScriptName} NAME )
    CopyFile( ${ScriptName} ${OutSubjDir}/${PROCESS_SUBDIR}/${name} )
  Endif( ${ScriptName} != '' )
  set( MD ${InSubjDir}/${i}${POPULATION_AVERAGE_INPUT_SUFFIX} )
  set( MASK ${InSubjDir}/${i}${NEW_MASK_TAG} )#we save mask in case directory
  set( maskedMD ${tempDir}/${i}${POPULATION_AVERAGE_INPUT_MASKED_TAG}.nrrd )
  set( Cmd ${ImageMath} ${MD} -mask ${MASK} -outfile ${maskedMD} -type float)
  echo( ${Cmd} )
  If( ${step} < 1 )
    run( output ${Cmd} )
    echo( ${output} )
  EndIf( ${step} < 1 )
  if( ${Mradius} != 0 )
    set( MedianMD ${tempDir}/${i}${POPULATION_AVERAGE_INPUT_MASKED_TAG}_median.nrrd )  
    set( Cmd ${MedianImageFilter} --neighborhood ${Mradius},${Mradius},${Mradius} ${maskedMD} ${MedianMD} )
    echo( ${Cmd} )
    If( ${step} < 2 )
      run( output ${Cmd} )
      echo( ${output} )
    EndIf( ${step} < 2 )
  else( ${Mradius} != 0 )
    set( MedianMD ${maskedMD} )
  endif( ${Mradius} != 0 )
  set( paddedMD ${tempDir}/${i}${POPULATION_AVERAGE_INPUT_MASKED_TAG}_padded.nrrd )
  set( Cmd ${unu} pad -i ${MedianMD} -o ${paddedMD} -min -10 -10 -10 -max M+10 M+10 M+10 -b pad )
  echo( ${Cmd} )
  If( ${step} < 3 )
    run( output ${Cmd} )
    echo( ${output} )
  EndIf( ${step} < 3 )
endforeach( ${CASES} )

set( POPULATION_AVERAGE_INPUT_MASKED_TAG ${POPULATION_AVERAGE_INPUT_MASKED_TAG}_padded )
#########################affine registration###############################
##affinely register all cases once they are skullstripped
GetParam(FIXED_CASE ${CASES} 0)
set( FIXED_IMG ${tempDir}/${FIXED_CASE}${POPULATION_AVERAGE_INPUT_MASKED_TAG}.nrrd )
set( RESCALED_FIXED_IMG ${tempDir}/${FIXED_CASE}${POPULATION_AVERAGE_INPUT_MASKED_TAG}_hm_resc.nrrd )
set( times 0 )
include( 031AffineRegistration.bms )

set( avgImage ${tempDir}/AverageAffineImage.nrrd )
set( Cmd ${ImageMath} ${FIRST_CASE} -avg ${ALL_OTHER_CASES} -outfile ${avgImage} )
If( ${step} < 9 )
  Run( output ${Cmd} )
  echo( ${output} )
EndIf( ${step} < 9 )
set(FIXED_CASE AverageAffineImage )
set( FIXED_IMG ${avgImage} )
set( RESCALED_FIXED_IMG ${tempDir}/AverageAffineImage_resc.nrrd )
include( 031AffineRegistration.bms )
####################Create average##############################################
MakeDirectory( ${outputDir} )
##Saves config file in average dir
if( ${ScriptName} != '' )
  ExitOnError( 'TRUE' )
  GetFilename( name ${ScriptName} NAME )
  CopyFile( ${ScriptName} ${outputDir}/${name} )
  ExitOnError( 'FALSE' )
Endif( ${ScriptName} != '' )
include( 034CreateXML.bms )

set ( atlasWerksCmd ${AtlasWerks} -f ${tempDir}/AW.xml)
MakeDirectory( ${outputDir}/Average )

If( ${step} < 15 )
  run ( output ${atlasWerksCmd})
  echo ( ${output} )
Endif( ${step} < 15 )

###################Save H-Fields#########################################
set( count 0 )
foreach( i ${CASES} )
  set (OutSubjDir ${dir}/${OUTPUT_PRE_SUBDIR}/${i})
  if( ${TRANSFORM_SUFFIX} != '' )
    set( combinedTransforms -f ${dir}/${INPUT_PRE_SUBDIR}/${i}/${INPUT_POST_SUBDIR}/${i}${TRANSFORM_SUFFIX} )
  else( ${TRANSFORM_SUFFIX} != '' )
    set( combinedTransforms '' )
  Endif( ${TRANSFORM_SUFFIX} != '' )
  If( ${count} < 10 )
    set( suffix 000${count} )
  Endif( ${count} )
  if( ${count} >= 10 AND ${count} < 100 )
    set( suffix 00${count} )
  Endif( ${count} )
  if( ${count} >= 100 AND ${count} < 1000 )
    set( suffix 0${count} )
  Endif( ${count} )
  if( ${count} >= 1000 )
    set( suffix ${count} )
  Endif( ${count} )
  set( hfield ${tempDir}/h_${suffix}.mhd )
  Set( COPIED_HFIELD ${OutSubjDir}/${PROCESS_SUBDIR}/${i}_hFieldToPopAverage.mha )
  Set( Cmd ${ResampleVolume2} ${hfield} ${COPIED_HFIELD} -i nn )
  If( ${step} < 16 )
    Run( output ${Cmd} )
    echo( ${output} )
  EndIf( ${step} < 16 )
  set( invhfield ${tempDir}/inv_h_${suffix}.mhd )
  Set( COPIED_INV_HFIELD ${OutSubjDir}/${PROCESS_SUBDIR}/${i}_inv_hFieldToPopAverage.mha )
  Set( Cmd ${ResampleVolume2} ${invhfield} ${COPIED_INV_HFIELD} -i nn )
  If( ${step} < 17 )
    Run( output ${Cmd} )
    echo( ${output} )
  EndIf( ${step} < 17 )

  Inc(${count} 1)
  Int( ${count} )
endforeach( ${CASES} )
########################################################################
if( ${IS_SCALAR} != TRUE )
  include( 032TransformDTI.bms )
else( ${IS_SCALAR} != TRUE )
  include( 033Histogram.bms )
Endif( ${IS_SCALAR} != TRUE )

GetParam(SCALED_CASE ${CASES} 0)
IF( ${IS_SCALED} == TRUE )
  If( ${step} < 45 )
    CopyFile(${dir}/${INPUT_PRE_SUBDIR}/${SCALED_CASE}/${INPUT_POST_SUBDIR}/${IMAGE_GRID} ${outputDir}/ImageGrid.nrrd  )
  EndIf( ${step} < 45 )
ENDIF( ${IS_SCALED} == TRUE )

Set( ImageForMask ${tempDir}/avg_2.mhd )
Set( ImageForMaskNrrd ${tempDir}/avg_2.nrrd )
Set( Mask ${tempDir}/mask.nrrd )

Set( Cmd ${ResampleVolume2} ${ImageForMask} ${ImageForMaskNrrd} -i nn )

Run( output ${Cmd} )
echo( ${output} )

Set( Cmd ${unu} 1op if -i ${ImageForMaskNrrd} -o ${Mask} )
If( ${step} < 46 )
  Run( output ${Cmd} )
  echo( ${output} )
EndIf( ${step} < 46 )

Set( SmoothMask ${outputDir}/Mask.nrrd )
Set( Cmd ${MedianImageFilter} ${Mask} --neighborhood 2,2,2 ${SmoothMask} )
If( ${step} < 47 )
  Run( output ${Cmd} )
  echo( ${output} )
EndIf( ${step} < 47 )
